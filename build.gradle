plugins {
    id "org.springframework.boot" version "2.7.1" apply false
    id "io.spring.dependency-management" version "1.0.11.RELEASE" apply false
}

allprojects {

    group "com.alexbob.platform"

    ext {
        hutoolVersion = "5.7.+"
        weixinVersion = "4.2.+"
        springCloudVersion = "2021.0.+"
        springCloudAlibabaVersion = "2021.0.+"
        springdocVersion = "1.6.+"
        minioVersion = "8.3.+"
    }

    repositories {
        maven { url "https://maven.aliyun.com/repository/public" }
        maven { url "https://maven.aliyun.com/repository/spring" }
        maven { url "https://maven.aliyun.com/repository/google" }
        mavenLocal()
        mavenCentral()
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, "minutes"
    }

}

subprojects {

    apply plugin: "java"
    apply plugin: "idea"
    apply plugin: "io.spring.dependency-management"

    sourceCompatibility = "17"

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    idea.module {
        outputDir = file("$buildDir/out/")
        testOutputDir = file("$buildDir/test")
    }

    dependencies {

        implementation "org.springdoc:springdoc-openapi-webflux-ui:${springdocVersion}"

        implementation "org.springframework.boot:spring-boot-starter-actuator"
        implementation "org.springframework.boot:spring-boot-starter-webflux"
        implementation "org.springframework.boot:spring-boot-starter-validation"
        implementation "org.springframework.boot:spring-boot-starter-data-redis-reactive"

        implementation "com.alibaba.cloud:spring-cloud-starter-alibaba-nacos-config"
        implementation "com.alibaba.cloud:spring-cloud-starter-alibaba-nacos-discovery"
        implementation "org.springframework.cloud:spring-cloud-starter-loadbalancer"
        implementation "com.github.ben-manes.caffeine:caffeine"

        compileOnly "org.projectlombok:lombok"
        annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
        annotationProcessor "org.projectlombok:lombok"

        testImplementation("org.springframework.boot:spring-boot-starter-test")
        testImplementation "io.projectreactor:reactor-test"

    }
    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:${springCloudAlibabaVersion}"
        }
    }

    test {
        useJUnitPlatform()
    }
}